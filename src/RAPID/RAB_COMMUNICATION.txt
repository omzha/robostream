MODULE RAB_COMMUNICATION
    RECORD target_speed_data
        !Custom data type
        robtarget target;
        speeddata speed;
    ENDRECORD
    
    VAR bool receiving_points := TRUE;
    VAR bool position_sent := FALSE;
    VAR bool position_reached := FALSE;
    
    VAR target_speed_data ts_data;
    VAR robtarget target_data;
    VAR jointtarget joint_target_data;
    
    !VAR robtarget received_pos;
    VAR speeddata Speed_01 := [90, 90, 5000, 1000];
    
    VAR zonedata Zone_01 := [FALSE, 5, 0, 0, 0, 0, 0];
    CONST robjoint HOME_End_Joint_1 := [0, 15, 25, 0, -55, 0];
    CONST robjoint HOME_Start_Joint_0 := [0, 15, 25, 0, -55, 0];
    VAR jointtarget HOME_Start_0 := [HOME_Start_Joint_0, [9E9, 9E9, 9E9, 9E9, 9E9, 9E9]];
    VAR jointtarget HOME_End_1 := [HOME_End_Joint_1, [9E9, 9E9, 9E9, 9E9, 9E9, 9E9]];
    
    VAR intnum target_speed_connectnum;
    VAR intnum target_connectnum;
    VAR intnum joint_target_connectnum;
    VAR intnum exit_connectnum;
    
    PROC main()
        ConfL\off;
        ConfJ\off;
        MoveAbsJ HOME_Start_0, Speed_01, Zone_01, zHDPXL\WObj:=wobj0;
        
        CONNECT exit_connectnum WITH RABMsgs;
        IRMQMessage receiving_points, exit_connectnum;
        CONNECT target_connectnum WITH RABMsgs;
        IRMQMessage target_data, target_connectnum;
        CONNECT joint_target_connectnum WITH RABMsgs;
        IRMQMessage joint_target_data, joint_target_connectnum;
        CONNECT target_speed_connectnum WITH RABMsgs;
        IRMQMessage ts_data, target_speed_connectnum;
        
        WHILE receiving_points = TRUE DO
            position_sent := FALSE;
            WHILE position_sent = FALSE DO
                WaitTime 3;
            ENDWHILE
            
        ENDWHILE
       
        IDelete exit_connectnum;
        IDelete target_connectnum;
        IDelete joint_target_connectnum;
        IDelete target_speed_connectnum;
        EXIT;
        
    ENDPROC
    TRAP RABMsgs
        VAR rmqmessage msg;
        VAR rmqheader header;
        VAR rmqslot rabclient;
        VAR num userdef;
        VAR string tar := "Target Received";
        VAR string ok := "Program End";
        VAR string reached := "Target Reached, waiting...";
        RMQGetMessage msg;
        RMQGetMsgHeader msg \Header:=header
            \SenderId:=rabclient\UserDef:=userdef;
        !check data type and assign value to flag variable
        
        IF header.datatype = "target_speed_data" THEN
            RMQGetMsgData msg, ts_data;
            position_sent := TRUE;
            !return receipt to sender
            RMQSendMessage rabclient, tar, \UserDef:=StrLen(tar);
            MoveL ts_data.target, ts_data.speed, Zone_01, zHDPXL\WObj:=wobj0;
            RMQSendMessage rabclient, reached, \UserDef:=StrLen(reached);
            
        ELSEIF header.datatype = "robtarget" THEN
            RMQGetMsgData msg, target_data;
            position_sent := TRUE;
            !return receipt to sender
            RMQSendMessage rabclient, tar, \UserDef:=StrLen(tar);
            MoveL target_data, Speed_01, Zone_01, zHDPXL\WObj:=wobj0;
            RMQSendMessage rabclient, reached, \UserDef:=StrLen(reached);

	ELSEIF header.datatype = "jointtarget" THEN
            RMQGetMsgData msg, joint_target_data;
            position_sent := TRUE;
            !return receipt to sender
            RMQSendMessage rabclient, tar, \UserDef:=StrLen(tar);
            MoveAbsJ joint_target_data, Speed_01, Zone_01, zHDPXL\WObj:=wobj0;
            RMQSendMessage rabclient, reached, \UserDef:=StrLen(reached);
            
        ELSEIF header.datatype = "bool" THEN
            RMQGetMsgData msg, receiving_points;
            position_sent := TRUE;
            !return receipt to sender
            MoveAbsJ HOME_End_1, Speed_01, Zone_01, zHDPXL\WObj:=wobj0;
            RMQSendMessage rabclient, ok;
            
        ELSE
            TPWrite "Unknown data received in PositionRABMsgs...";
        ENDIF
    ENDTRAP
ENDMODULE